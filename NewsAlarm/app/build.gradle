plugins {
    id 'com.android.application'
}

android {
    namespace 'com.alarm.newsalarm'
    compileSdk = 36

    defaultConfig {
        applicationId "com.alarm.newsalarm"
        minSdk 33
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.current()
        targetCompatibility JavaVersion.current()
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def apk = output.outputFile
            def name = parent.project.getName()
            def buildType = variant.buildType.name
            def versionName = variant.versionName
            def newName

            if (apk.name.startsWith("app-debug")) {
                newName = apk.name.replace("app-debug", "${name}_${versionName}-${buildType}")
            } else {
                newName = apk.name.replace("app-release", "${name}_${versionName}-${buildType}")
            }

            outputFileName = newName
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.work:work-runtime:2.11.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.room:room-runtime:2.8.3'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'org.jsoup:jsoup:1.21.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.room:room-testing:2.8.3'

    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

    annotationProcessor 'androidx.room:room-compiler:2.8.3'
}
